config wrm {
    label = "Warrior, Rogue & Mage"
    id = "wrm"
    description = "Custom ISDL Build of Warrior, Rogue & Mage"
    author = "Roman Trevisanut"
}

actor Character {
    // PAGE 1 - CHARACTER DEFAULT
    section Attributes {
        //WARRIOR
        attribute Warrior(min: 0, max: 100)
        // ROGUE
        attribute Rogue(min: 0, max: 100)
        // MAGE
        attribute Mage(min: 0, max: 100)
    }
    //section Resources {
    //    // Calculate total health for the character.
    //}
    section QuickActions {
        action HPReset(icon: "fa-solid fa-clover", disabled: self.Health == self.Health.Max) {
            self.Health = self.Health.Max
            chat HealthReset {"I've reset my Health points to " self.Health + "."}
            }
        action FPReset(icon: "fa-solid fa-clover", disabled: self.Fate == self.Fate.Max) {
            self.Fate = self.Fate.Max
            chat FateReset {"I've reset my Fate points to " self.Fate + "."}
            }
        action MPReset(icon: "fa-solid fa-clover", disabled: self.Mana == self.Mana.Max) {
            self.Mana = self.Mana.Max
            chat ManaReset {"I've reset my Mana points to " self.Mana + "."}
            }
        }
    section ImportantNumbers {
        number Advances(min: 0)
        number BonusAdv(min: 0)
        hidden number DefenseMod(initial: 0)
        hidden number CastPenaltyMod(initial: 0)
        hidden number FullMovement(initial: 10)
        hidden number ShortMovement(initial: 5)
        hidden number ManaMult(initial: 2)
    }
    Advancement[] Advancements
    Skill[] Skills
    Talent[] Talents
    Language[] Languages
    Project[] Projects
    // Page 2 -- Belongings
    page Belongings(icon: "fa-solid fa-backpack") {
    section Armor {
        //Equipment WornHelmet
        unlocked string Cleanliness(choices: ["Pristine","Clean","Grubby","Dirty","Grimy","Filthy"])
        choice<Armor> WornArmor
        choice<Armor> WornShield
        hidden number ShieldBonus(value: {
            if (self.WornShield exists) {
                return self.WornShield.system.armorrating
                }
                return 0
                })
    }
        //    paperdoll Clothing(image: "systems/wrm/img/paperdoll_lowpoly_dual.png", size: 50px) {
        //        // LEFT - Clothing and Accessories
        //        Equipment Clothing(left: 47px, top: 60px)
        //        Equipment Legwear(left: 47px, top: 139px)
        //        Equipment Belt(left: 115px, top: 100px)
        //        Equipment Outerwear(left: 0px, top: 60px)
        //        Equipment Footwear(left: 47px, top: 215px)
        //        Equipment AccessoryI(left: 0px, top: 0px)
        //        Equipment AccessoryII(left: 43px, top: 0px)
        //        Equipment AccessoryIII(left: 86px, top: 0px)
        //        // RIGHT - Armor and Weapons
        //        Armor ArmorHelmet(left: 187px, top: 13px)
        //        Armor ArmorBody(left: 187px, top: 53px)
        //        Armor ArmorShoulders(left: 140px, top: 53px)
        //        Armor ArmorArms(left: 140px, top: 96px)
        //        Armor ArmorLegs(left: 187px, top: 139px)
        //        Armor ArmorShield(left: 250px, top: 103px)
        //        Weapon WeaponI(left: 250px, top: 60px)
        //        Weapon WeaponII(left: 187px, top: 103px)
        //        Weapon WeaponIII(left: 250px, top: 146px)
        //        }
    section Finance {
        unlocked number Silver
        unlocked number BankedSilver
        number Investments
        readonly number MonthlyIncome(value: {return 300 * self.Investments})
    }
    section CostOfLiving {
        unlocked string LodgingQuality(choices:["Free","Bare","Modest","Good","Rich","Extravagant"])
        hidden number LodgingQualityCost(value: {
            if (self.LodgingQuality == "Extravagant") {return 800}
            if (self.LodgingQuality == "Rich") {return 400}
            if (self.LodgingQuality == "Good") {return 200}
            if (self.LodgingQuality == "Modest") {return 100}
            if (self.LodgingQuality == "Bare") {return 50}
            else {return 0}
        })
        unlocked string MealQuality(choices:["Free","Bare","Modest","Good","Rich","Extravagant"])
        hidden number MealQualityCost(value: {
            if (self.MealQuality == "Extravagant") {return 160}
            if (self.MealQuality == "Rich") {return 80}
            if (self.MealQuality == "Good") {return 40}
            if (self.MealQuality == "Modest") {return 20}
            if (self.MealQuality == "Bare") {return 10}
            else {return 0}
        })
        string Breakdown(value: {return self.LodgingQualityCost + "sp + " + self.MealQualityCost + "sp"})
        unlocked number Days(min:1)
        readonly number Cost(value: {return (self.LodgingQualityCost + self.MealQualityCost) * self.Days})
        action PayCash(icon: "fa-duotone fa-solid fa-coins", disabled: self.Silver < self.Cost) {
            self.Silver -= self.Cost
            self.update()
            chat Paid {
                flavor ("I paid " + self.Cost + " Silver out of pocket for cost of living!" )
                tag self.Cost
                }
        }
        action UseBank(icon: "fa-solid fa-building-columns", disabled: self.BankedSilver < self.Cost) {
            self.BankedSilver -= self.Cost
            self.update()
            chat Paid {
                flavor ("I paid " + self.Cost + " Silver from my bank for cost of living!" )
                tag self.Cost
                }
            }
    }
    Armor[] Armors
    Consumable[] Consumables
    Equipment[] Wearables
    Item[] Items
    Weapon[] Weapons
    }
    // PAGE 3 -- COMBAT
    page Combat(icon: "fa-solid fa-swords") {
        health resource Health(max: {return 6 + self.Warrior.value})
        resource Fate(max: {
            if (self.Rogue == 0) {return 1}
            else {return self.Rogue}
            })
        resource Mana(max: {return self.Mage * self.ManaMult})
    section Defenses {
        readonly string Movement(value: {return self.FullMovement + " Yds (" + self.ShortMovement + " w/ action)"})
        readonly number Dodge(value: {return 3 + ((self.Warrior + self.Rogue + self.Mage) / 3) + self.ShieldBonus + self.DefenseMod})
        number ArmorPenalty(min: 0, value: {
            if (self.WornArmor exists) {
                return ((self.WornArmor.system.armorrating - 1) + self.ShieldPenalty + self.CastPenaltyMod)
                }
                return 0
                })
        hidden number ShieldPenalty(min: 0, value: {
            if (self.WornShield exists) {
                return (self.WornShield.system.armorrating - 1)
                }
                return 0
                })
        readonly string ArmorAbsorb(value: {
            if (self.WornArmor exists) {
                return self.WornArmor.system.armorabsorb + " (" + self.WornArmor.system.armorcondition + ")"
                }
                return "N/A"
                })
        readonly string ShieldBlock(value: {
            if (self.WornShield exists) {
                return self.WornShield.system.armorrating + " (" + self.WornShield.system.armorcondition + ")"
                }
                return "N/A"
                })
    }
    section Rollers {
                action WARRIOR(icon: "fa-solid fa-helmet-battle"){
            fleeting Prompt = prompt(label: "WARRIOR Roll Modifiers") {
                string Advantage(choices: ["None","Advantage","Disadvantage"])
                number AddModifier
            }
            fleeting Modifier = Prompt.AddModifier
            fleeting Roll = roll(1d6 + self.Warrior + Modifier)
            //if (Prompt.Advantage == "None") {Roll = roll(1d6 + self.Warrior)}
            if (Prompt.Advantage == "Advantage") {Roll = roll(2d6kh + self.Warrior + Modifier)}
            if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6kl + self.Warrior + Modifier)}
            chat RollResult {
                flavor("Rolling a basic WARRIOR check.")
                Roll
            }
        }
        action ROGUE(icon: "fa-solid fa-hood-cloak"){
            fleeting Prompt = prompt(label: "ROGUE Roll Modifiers") {
                string Advantage(choices: ["None","Advantage","Disadvantage"])
                number AddModifier
            }
            fleeting Modifier = Prompt.AddModifier
            fleeting Roll = roll(1d6 + self.Rogue + Modifier)
            if (Prompt.Advantage == "Advantage") {Roll = roll(2d6kh + self.Rogue + Modifier)}
            if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6kl + self.Rogue + Modifier)}
            chat RollResult {
                flavor("Rolling a basic ROGUE check.")
                Roll
            }
        }
        action MAGE(icon: "fa-solid fa-hat-wizard"){
            fleeting Prompt = prompt(label: "MAGE Roll Modifiers") {
                string Advantage(choices: ["None","Advantage","Disadvantage"])
                number AddModifier
            }
            fleeting Modifier = Prompt.AddModifier
            fleeting Roll = roll(1d6 + self.Mage + Modifier)
            if (Prompt.Advantage == "Advantage") {Roll = roll(2d6kh + self.Mage + Modifier)}
            if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6kl + self.Mage + Modifier)}
            chat RollResult {
                flavor("Rolling a basic MAGE check.")
                Roll
            }
        }
    }
    section Actions {
        action YesOrNo(icon: "fa-solid fa-circle-question"){
            fleeting flip = roll(1d6)
            fleeting Answer = "??"
            if (flip.Total > 3) {Answer = "Yes"}
            else {Answer = "No"}
            chat Output {
                flavor ("The real question is: Yes or No?")
                Answer
                }
        }
        action Coin(icon: "fa-solid fa-coin"){
            fleeting flip = roll(1d2)
            fleeting Result = "??"
            if (flip.Total == 2) {Result = "Heads"}
            else {Result = "Tails"}
            chat Output {
                flavor ("The coin's up!")
                Result
                }
        }
        action Rating(icon: "fa-solid fa-star"){
            fleeting Score = roll(d10)
            chat Output {
                flavor ("On a scale from 1 to 10 you give this a...")
                Score
                }
        }
        action Talents(icon: "fa-solid fa-arrows-rotate-reverse"){
                   each talent in self.Talents {
                       if (talent.UsesLeft < talent.MaxUses) {
                        talent.system.usesleft = talent.MaxUses
                       }
                   }
                   chat TalentReset {"I've reset all my talent uses to full."}
         }
        // action ResetTurn(icon:"fa-solid fa-circle-three-quarters-stroke"){
        //    chat Reset{flavor("Resetting all per-turn talent uses. TODO")}
        // }
        //action ResetBattle(icon:"fa-solid fa-circle-quarter-stroke"){
        //    chat Reset{flavor("Resetting all per-battle/scene talent uses. TODO")}
        // }
        //action Aaaaaaaa(icon:"fa-solid fa-person-harassing"){
        //    chat Reset{flavor("AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA")}
        // }
        // action DamageShield(icon:"fa-solid fa-shield-slash"){
        //     chat Damage{flavor("Shield damaged.")}
        // }
        // action DamageArmor(icon:"fa-solid fa-claw-marks", disabled: self.WornArmor !exists){
        //     chat Damage{flavor("Armor damaged.")}
        // }
        // action DamageHelmet(icon:"fa-solid fa-helmet-battle"){
        //    chat Damage{flavor("Helmet damaged.")}
        // }
    }

    Armor[] Armors
    Consumable[] Consumables
    Skill[] Skills
    Talent[] Talents
    Weapon[] Weapons
    }
    // Page 4 -- MAGIC
    page Magic(icon: "fa-solid fa-sparkles") {
        section SpellSelection {
            unlocked choice<Spell> CastSpell
            unlocked choice<Skill> CastingSkill
            string SpellInfo(value:{
                if (self.CastSpell exists) {return "CIR: " + self.CastSpell.system.circle + ", MP: " + self.CastSpell.system.mana + ", DL: " + self.CastSpell.system.difficulty}
                else {return "No Spell Chosen"}
            })
            string SkillInfo(value:{
                if (self.CastingSkill exists) {return "Level: " + self.CastingSkill.system.skilllevel + ", Bonus: " + self.CastingSkill.system.skilltotal}
                else {return "No Skill Chosen"}
            })
            hidden number CastingSkillBonus(value: {
                if (self.CastingSkill exists) {return self.CastingSkill.system.skilltotal}
                else {return 0}
            })
            hidden number SpellCircle(value: {
                if (self.CastSpell exists) {return self.CastSpell.system.circle}
                else {return 0}
            })
            hidden number ManaCost(value: {
                if (self.CastSpell exists) {return self.CastSpell.system.mana}
                else {return 0}
            })
            hidden number DL(value: {
                if (self.CastSpell exists) {return self.CastSpell.system.difficulty}
                else {return 0}
            })
        }
        section CastOverview {
/// LETS CLEAN THIS UP
            unlocked number Enhancements
            unlocked number FreeEnhances
            unlocked number CostMod
            hidden number TotalEnhance(value: {return self.Enhancements + self.FreeEnhances})
            hidden number EnhanceCost(value: {
                if (self.CastSpell exists) {return Math.round(self.ManaCost / 2 * self.Enhancements)}
                else {return 0}
            })
            hidden number EnhanceDL(value: {
                if (self.CastSpell exists) {return self.Enhancements}
                else {return 0}
            })
            readonly number CastPenalty(value: {return Math.round(self.ArmorPenalty / 2)})
            hidden number FinalMana(value: {
                if (self.CastSpell exists) {return self.ManaCost + self.CastPenalty + self.EnhanceCost + self.CostMod}
                else {return 0}
            })
            hidden number FinalDL(value: {
                if (self.CastSpell exists) {return self.DL + self.CastPenalty + self.EnhanceDL}
                else {return 0}
            })
            string DLBreakdown(value: {return "DL: " + self.FinalDL + " (Base: " + self.DL + " Enh: +" + self.EnhanceDL + " Pen: +" + self.CastPenalty + ")"})
            string MPBreakdown(value: {return "MP: " + self.FinalMana + " (Base: " + self.ManaCost + " Enh: +" + self.EnhanceCost + " Pen: +" + self.CastPenalty + " Mod: +" + self.CostMod + ")"})
        }
        section SpellRoller {
// SPELL ROLLER HERE
            unlocked string Ability(choices: ["Warrior","Rogue","Mage"])
            //unlocked string Advantage(choices: ["None","Advantage","Disadvantage"])
            hidden number CastBase(value:{
                if (self.CastingSkill exists) {return self.CastingSkill.system.skillbase}
                else {return 0}
            })
            unlocked number CastBonus(value:{
                if (self.CastingSkill exists) {return self.CastingSkill.system.skilltotal}
                else {return 0}
            })
            //unlocked number CastModifier
            unlocked string Resource(choices:["Mana","Health","Other"])
/// NU ROLLER HERE YEEEEEEEEEEEEA BAYBEEEEEEEE
        action CastSpell(icon: "fa-solid fa-hand-sparkles"){
            // SORT OUT TAGS
            fleeting Level = "None"
            if (self.CastingSkill exists) {Level = self.CastingSkill.system.skilllevel}
            fleeting CastSkill = "None"
            if (self.CastingSkill exists) {CastSkill = self.CastingSkill.name}
            fleeting Skill = self.CastingSkillBonus
            fleeting Prompt = prompt(label: "Casting Roll Modifiers") {
                string Advantage(choices: ["None","Advantage","Disadvantage"])
                number AddModifier
            }
            fleeting Modifier = Prompt.AddModifier
            fleeting Roll = roll(1d6 + self[self.Ability].mod + Skill + Modifier)
            if (Level == "None") {
                if (Prompt.Advantage == "Advantage") {Roll = roll(2d6kh + self[self.Ability].mod + Skill + Modifier)}
                else if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6kl + self[self.Ability].mod + Skill + Modifier)}
                else {Roll = roll(1d6 + self[self.Ability].mod + Skill + Modifier)}
            }
            else {
                if (Prompt.Advantage == "Advantage") {Roll = roll(2d6xkh + self[self.Ability].mod + Skill + Modifier)}
                else if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6xkl + self[self.Ability].mod + Skill + Modifier)}
                else {Roll = roll(1d6x + self[self.Ability].mod + Skill + Modifier)}
            }
            chat RollResult {
                flavor("Rolling to cast a spell!")
                Roll
                tag self.Ability
                tag Level
                tag CastSkill
            }
           }
        }
    Spell[] Spells
    Skill[] Skills
    Talent[] Talents
    Consumable[] Consumables
    }
    page ItemLists(icon: "fa-solid fa-table-list") {
    Armor[] Armors
    Consumable[] Consumables
    Equipment[] Equipables
    Item[] Items
    Weapon[] Weapons
    }
    page TestZone(icon: "fa-solid fa-grid-2") {
        string What
}
}

//////// ITEMS BEGIN HERE
///////
/////
///
//

// LANGUAGE
item Language(svg: "icons/svg/quill-ink.svg", description: "A useful language.") {
    string Kind(choices: ["Common", "Uncommon", "Rare", "Dead", "Ancient"])
    string Fluency(choices: ["Native", "Conversant", "Fluent", "Broken", "Academic"])
}

// TALENT
item Talent(svg: "icons/svg/upgrade.svg", description: "A useful ability or improvement.") {
    section Details {
        string PreReq
        string Kind(choices: ["Standard","Inherent","Free","Special","Negative","Temporary"])
    }
    section Tracking {
        tracker Combo(style: icons, icon: "fa-hand-back-fist", min: 0, max: {return self.MaxUses}, visibility: {
            if (!self.HasCombo) {return Visibility.hidden}
            })
        tracker Resource(min: 0, max: {return self.MaxUses}, visibility: {
            if (!self.HasResource) {return Visibility.hidden}
            })
        tracker Uses(min: 0, max: {return self.MaxUses}, visibility: {
            if (!self.HasFreeUses) {return Visibility.hidden}
            })
    }
    section Usage {
        string Trigger
        string Requirement
        string Activation(choices: ["Passive", "Action", "Reaction", "Special"])
        string UseLimit(choices: ["Passive","At-Will","Turn","Round","Battle","Day","Session"])
        //action UseTalent(icon: "fa-solid fa-play", disabled: self.HasFreeUses == false) {
        //    self.UsesLeft -= 1
        //    self.update()
        //    chat TalentUse {
        //        tag self.Activation
        //        }
        //}
    }
    section Config {
        boolean HasCombo
        number MaxCombo(min: 0)
        boolean HasResource
        number MaxResource
        boolean HasFreeUses
        number MaxUses(min: 0)
    }
}

//SKILL
item Skill(svg: "icons/svg/weight-lifting-up.svg", description: "A useful bit of training.") {
    section Rating {
        string CoreAbility(choices: [ "Warrior", "Rogue", "Mage" ])
        string SkillLevel(choices: [ "None","Trained","Expert","Master" ])
        hidden number SkillBase(value: {
            if (self.SkillLevel == "Master") {return 4}
            if (self.SkillLevel == "Expert") {return 3}
            if (self.SkillLevel == "Trained") {return 2}
            if (self.SkillLevel == "None") {return 0}
            return 0
        })
        number SkillMod
        readonly number SkillTotal(value: {return self.SkillBase + self.SkillMod})
    }
    section RollConfig {
        unlocked parent<attribute> Ability(choices: [Character.Warrior, Character.Rogue, Character.Mage])
    // ROLLERS
        action Roll(icon: "fa-solid fa-dice-five") {
            fleeting Prompt = prompt(label: "Roll Modifiers") {
                string Advantage(choices: ["None","Advantage","Disadvantage"])
                number AddModifier
            }
            fleeting Modifier = Prompt.AddModifier
            fleeting Skill = self.SkillTotal
            fleeting Roll = roll(1d6 + self.Ability + Skill + Modifier)
            if (self.SkillLevel == "None") {
                if (Prompt.Advantage == "Advantage") {Roll = roll(2d6kh + self.Ability + Skill + Modifier)}
                else if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6kl + self.Ability + Skill + Modifier)}
                else {Roll = roll(1d6 + self.Ability + Skill + Modifier)}
            }
            else {
                if (Prompt.Advantage == "Advantage") {Roll = roll(2d6xkh + self.Ability + Skill + Modifier)}
                else if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6xkl + self.Ability + Skill + Modifier)}
                else {Roll = roll(1d6x + self.Ability + Skill + Modifier)}
            }
            chat RollOutcome {
                Roll
                tag self.Ability
                tag self.SkillLevel
            }
        }
    }
}

// CONSUMABLE
item Consumable(svg: "icons/svg/square-bottle.svg", description: "An item that is consumed on use.") {
    section Details {
        string Kind(choices:["Ammo","Food","Elixir","Ration","Light","Liquor","Other"])
        number Value(min:0)
        boolean Refillable
        number Uses(min:0)
    }
    section Uses {
        string UseLimit(choices: ["N/A", "At-Will", "Turn", "Round", "Battle", "Day", "Session"])
        action Use(icon: "fa-solid fa-flask-round-potion") {
            self.Uses --
            chat PotionUsed {}
            self.update()
            // If it's a refill container, do not self delete when Uses are 0.
            // If it's non-refill, self delete when Uses are 0.
            if (self.Refillable == false) {
                if (self.Uses == 0) {self.delete()}
                }
        }
    }
}

// WEAPON
item Weapon(svg: "icons/svg/saber-and-pistol.svg", description: "A piece of offensive gear.") {
    section Qualities {
        number Value(min:0)
        boolean TwoHanded
        boolean MeleeWeapon
        boolean RangedWeapon
        boolean ThrownWeapon
        number IdealRange(min: 0)
        number MaxRange(min: 0)
    }
    section Use {
        parent<attribute> Ability(choices: [Character.Warrior, Character.Rogue, Character.Mage])
        string Skill
        number AttackMod

    // ROLLERS
        action Attack(icon: "fa-solid fa-dice-five") {
            fleeting Prompt = prompt(label: "Roll Modifiers") {
                string Advantage(choices: ["None","Advantage","Disadvantage"])
                number AddModifier
            }
            fleeting Modifier = Prompt.AddModifier + self.AttackMod
            fleeting range = "Melee"
            if (self.RangedWeapon == true) {
                range = self.IdealRange + " Yds"
            }
            // See if the character has the needed Skill.
            fleeting Level = "None"
            fleeting Skill = 0
            if (parent is Character) {
                each skill in parent.Skills {
                    if (skill.Name equals self.Skill) {
                        Level = skill.SkillLevel
                        Skill = skill.SkillTotal
                    }
                }
            }
            fleeting Roll = roll(1d6 + self.Ability + Skill + Modifier)
            if (Level == "None") {
                if (Prompt.Advantage == "Advantage") {Roll = roll(2d6kh + self.Ability + Skill + Modifier)}
                else if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6kl + self.Ability + Skill + Modifier)}
                else {Roll = roll(1d6 + self.Ability + Skill + Modifier)}
            }
            else {
                if (Prompt.Advantage == "Advantage") {Roll = roll(2d6xkh + self.Ability + Skill + Modifier)}
                else if (Prompt.Advantage == "Disadvantage") {Roll = roll(2d6xkl + self.Ability + Skill + Modifier)}
                else {Roll = roll(1d6x + self.Ability + Skill + Modifier)}
            }
            chat RollOutcome {
                Roll
                tag self.Ability
                tag self.Skill
                tag range
            }
        }
    }
    section Damage {
        string DamageType(choices: ["Cutting", "Piercing", "Bludgeoning", "Ballistic", "Explosive", "Magic", "Psychic", "Solar", "Fire", "Ice", "Corrosive", "Poison", "Spirit"])
        number DiceCount(min: 1)
        string Limit(choices: [ "None","Max5","Max4","Max3","Max2"])
        number DamageMod
        hidden string DamageRoll(value:{
            if (self.Limit == "None") {return self.DiceCount + "d6x"}
            else {return self.DiceCount + "d6xmin1" + self.Limit}
            //if (self.Limit == "None") {return self.DiceCount + "d6xmin1"}
            //else if (self.Limit == "Max5") {return self.DiceCount + "d6xmin1max5"}
            //else if (self.Limit == "Max4") {return self.DiceCount + "d6xmin1max4"}
            //else if (self.Limit == "Max3") {return self.DiceCount + "d6xmin1max3"}
        })
    action Damage(icon: "fa-solid fa-heart-crack" ){
        fleeting damageRoll = roll(self.DamageRoll + self.DamageMod)
        chat AttackOutcome {
            damageRoll
            tag self.DamageType
            }
        }
    }
}

// ITEM
item Item(svg: "icons/svg/open-treasure-chest.svg", description: "A random item of dubious value.") {
    section Qualities {
        string Kind(choices: ["Book","Document","Material","Valuable","Tool","Other","Special"])
        boolean Magical
        number Value(min:0)
        number Quantity(min: 1)
    }
    section Details {
        string Status(choices: ["Carried","Accessible","Stored","Missing"])
        string Location
        string Condition(choices: ["New","Good","Worn","Battered","Damaged","Ruined","Destroyed","Pristine"])
    }
}

// SPELL
item Spell(svg: "icons/svg/magic-swirl.svg", description: "A magical ability you can cast.") {
    section Basics {
        number Circle(initial: 1, max: 7)
        number Difficulty(value: {
            @js{let spellDLScale = [0, 5, 7, 9, 13, 19, 25, 31];}
            return @js{spellDLScale[system.circle]}
        })
        number Mana(value: {
            @js{let spellMPScale = [0, 1, 2, 4, 8, 16, 32, 64];}
            return @js{spellMPScale[system.circle]}
        })
      //  number Targets(value: {
      //      @js{let spellTargetScale = [0, 1, 2, 3, 5, 8, 13, 21];}
      //      return @js{spellTargetScale[system.circle]}
      //      })
      //  number DamageDice(value: {return self.Circle})
      //  number DamageMod(value: {return self.Circle})
      //  number FlatDamage(value: {return self.Circle})
    }
    section Effect{
      //  string DamageClass(choices: ["Physical","Magical"])
        string DamageType(choices: ["Cutting", "Piercing", "Bludgeoning", "Ballistic", "Explosive", "Magic", "Psychic", "Solar", "Fire", "Ice", "Corrosive", "Poison", "Spirit"])
        boolean Intangible
        string Range(choices: ["Touch", "Ranged", "Line of Sight"])
        number Yards
    }
    section Other {
        string Source(choices:["Innate","Channeled","Planar"])
        string Field(choices: ["Forming", "Shaping", "Twisting", "Unaligned"])
      //  string Practice
      //  string Ability(choices: ["Warrior", "Rogue", "Mage"])
      //  string CastingSkill
        //parent<resource> Expend(choices: [Character.Mana,Character.Health])
        string Consumes(choices: ["Mana","Blood","Favor","Fate","Other"])
    }
}

// ADVANCEMENT
item Advancement(svg: "icons/svg/progression.svg", description: "A record of a character's growth.") {
    section Details {
    string AdvancementType(choices: ["Standard","Bonus"])
    string Adding(choices: ["Ability","Talent","Skill","Language","Health","Mana"])
    string Note
    }
    section Logging {
    date Awarded
    string IngameDate
    }
}

// ARMOR
item Armor(svg: "icons/svg/mail-shirt.svg", description: "A piece of protective gear") {
section Protection {
    number ArmorRating(max: 10)
    number ArmorState(min: 0, max: 7)
    hidden number ArmorStateMax(initial: 7)
    //string ArmorCondition(choices:["New","Good","Worn","Battered","Damaged","Ruined","Destroyed"])
    string ArmorCondition(value:{
        if (self.ArmorState == 7) {return "New"}
        if (self.ArmorState == 6) {return "Good"}
        if (self.ArmorState == 5) {return "Worn"}
        if (self.ArmorState == 4) {return "Battered"}
        if (self.ArmorState == 3) {return "Damaged"}
        if (self.ArmorState == 2) {return "Ruined"}
        if (self.ArmorState == 1) {return "Destroyed"}
        else {return "Destroyed"}
    })
    hidden number ArmorReduction(value: {
        if (self.ArmorCondition == "Damaged") {return Math.round(self.ArmorRating / 2)}
        if (self.ArmorCondition == "Ruined") {return Math.round(self.ArmorRating / 2)}
        if (self.ArmorCondition == "Destroyed") {return self.ArmorRating}
        else {return 0}
    })
    readonly number ArmorAbsorb(value: {return self.ArmorRating - self.ArmorReduction})
}
    section Actions {
        action Repair(icon:"fa-solid fa-hammer-crash") {
            self.ArmorState ++
            self.update()
            if (self.ArmorState > 7) {self.ArmorState = 7}
            chat ArmorRepaired {
                flavor ("Armor repaired!")
            }
        }
        action Damage(icon:"fa-solid fa-burst") {
            self.ArmorState --
            self.update()
            if (self.ArmorState < 1) {self.ArmorState = 1}
            chat ArmorDamaged {
                flavor("Armor damaged!")
                }
            }
    }
    section Tags {
        boolean Brittle
        boolean Heavy
        boolean Irreparable
        boolean Noisy
    }
    section Qualities {
        number SilverValue
        readonly string Repair(value: {
            if (self.Irreparable == true) {return "Magic Only"}
            else {return self.SilverValue / 10 + "s, " + self.ArmorRating / 2 + " days"}
            })
        string Shape(choices: ["Humanoid","Odakan","Cagran","Savran","Kurook","Other","Any"])
        }
}

// PROJECTS
item Project(svg: "icons/svg/scroll-quill.svg", description: "A task that takes time to complete.") {
    //string Difficulty(choices: ["Easy","Routine","Challenging","Hard","Extreme","Insane","Impossible"])
    //string Complexity(choices: ["Minimal","Moderate","Considerable","Heavy","Extreme","Insane","Immense"])
section Details {
    number Difficulty
    readonly number TaskDL(value: {return 5 + self.Difficulty})
    number Work
}
section Tracking {
    number Progress(max: {
    if (self.Work > 0) {return self.Work}
    else {return 1}
    })
    hidden number Done(value: {return (self.Progress / self.Work) * 100})
    readonly string Completion(value: {return self.Done + "%"})
    }
}

// EQUIPMENT
item Equipment(svg: "icons/svg/pirate-coat.svg", description: "A wearable piece of equipment.") {
    section Qualities {
        string Kind(choices: ["Headwear","Facewear","Cloak","Clothing","Legwear","Footwear","Neckwear","Belt","Bracelet","Gloves","Ring"])
        //string Kind(choices: ["OuterWear","Bracelet","Coat","Clothing","Ring","Footwear","Gloves","Belt","Headwear","Cloak","Neckwear"])
        string Shape(choices: ["Humanoid","Odakan","Cagran","Savran","Kurook","Other","Any"])
        string ClothingStyle(choices: ["Utility","Casual","Dress","Formal","Royal"])
        string Condition(choices: ["New","Good","Worn","Battered","Damaged","Ruined","Destroyed","Pristine"])
        string Origin
        boolean IsMagical
        number SilverValue(min:0)
    }
    //section Appearance {
        //string Quality(choices: ["Shoddy", "Decent", "Good", "Excellent", "Fantastic", "Amazing"])
        //string State(choices: ["New", "Decent", "Worn", "Damaged", "Ruined"])
        //string Condition(choices: ["Pristine", "Clean", "Stained", "Dirty", "Filthy"])
        //string Value(choices: ["Cheap", "Modest", "Good", "Rich", "Extravagant"])
        //string Structure(choices: ["New", "Decent", "Worn", "Damaged", "Ruined"])
        //string Appearance(choices: ["Pristine", "Clean", "Stained", "Dirty", "Filthy"])
    //}
}

// Follower
item Follower {
    section Abilities {
    number Warrior
    number Rogue
    number Mage
    }
}


//item Potion {
//    number Quantity
//    action Use(icon: "fa-solid fa-flask-round-potion") {
//        self.Quantity--
//        chat PotionUsed {}
//        self.update()
//
//        if (self.Quantity == 0) {
//            self.delete()
//        }
//    }
//}

actor Army {
    section Attributes {
        string Experience(choices: ["Untrained","Trained","Veteran","Elite"])
        string Class(choices: ["Light", "Medium", "Heavy"])
        string Weaponry(choices: ["Spears","Swords","Pikes","Bows"])
        string Movement(value: {
        if (self.Class == "Light") {return "Fast: 1/2/3"}
        if (self.Class == "Heavy") {return "Slow: 1/1/2"}
        if (self.Class == "Mounted") {return "Swift: 2/3/4"}
        else {return "Base: 1/2/2"}
        })
        hidden number ArmorBonus(initial: 0)
        number Armor(value: {
        if (self.Class == "Light") {return 3 + self.ArmorBonus}
        if (self.Class == "Heavy") {return 5 + self.ArmorBonus}
        else {return 4 + self.ArmorBonus}
    })
    }
    section Resources {
        resource Resolve(max: {
            if (self.Experience == "Trained") {return 8}
            if (self.Experience == "Veteran") {return 10}
            if (self.Experience == "Elite") {return 12}
            else {return 6}
        })
        resource Stamina(max: {
            if (self.Experience == "Trained") {return 8}
            if (self.Experience == "Veteran") {return 10}
            if (self.Experience == "Elite") {return 12}
            else {return 6}
        })
        health resource Health(max: {
            if (self.Experience == "Trained") {return 8}
            if (self.Experience == "Veteran") {return 10}
            if (self.Experience == "Elite") {return 12}
            else {return 6}
        })
    }
    section Actions {
        action Clash(icon: "fa-solid fa-swords", disabled: self.Stamina < 1){
                fleeting Prompt = prompt(label: "Clash Roll Modifiers") {
                    string Difficulty(choices: ["cs>=2","cs>=3","cs>=4","cs>=5"])
                    string Flanking(choices:["Difficulty -1 step if enemy Flanked."])
                    string Holding(choices:["Difficulty +1 step if enemy is Holding."])
                }
                fleeting dice = Math.round((self.Health + self.Stamina + self.Resolve) / 3)
                fleeting rolltext = dice + "d6x6" + Prompt.Difficulty
                fleeting Roll = roll(rolltext)
                self.Stamina --
                chat RollResult {
                    flavor("Rolling Clash check. Stamina reduced by 1.")
                    Roll
                }
            }
        action Rest(icon: "fa-solid fa-people-arrows"){
            self.Health ++
            self.Stamina ++
            self.Resolve ++
            chat RollResult {
                flavor("Unit has Rested. Health, Stamina, and Resolve improve by 1.")
            }
        }
        action Rally(icon: "fa-solid fa-person-military-pointing"){
                fleeting Roll = roll(1d6x6)
                self.Resolve += Roll.result
            chat RollResult {
                flavor("Unit has Rallied. Recovered 1d6 Resolve and ended the Routed condition.")
                Roll
            }
        }
        action Regroup(icon: "fa-solid fa-flag"){
            self.Health += 2
            self.Stamina += 2
            self.Resolve += 2
            chat RollResult {
                flavor("Unit has Regrouped. Spent 1 Reserve. Health restored, Stamina and Resolve improve by 2.")
            }
        }
    }
    ArmyTalent[] Talents
}

item ArmyTalent {
    section Configuration {
        string Effect
        string Activation(choices:["None", "As Turn", "When Clashing", "When Resting", "When Rallying", "When Holding", "When Moving"])
        number Dice(min: 1)
    }
    section Actions {
        //boolean DoesDamage
        string EffectType(choices: ["General","Damage"])
        action UseTalent(hidden: self.EffectType == "Damage", disabled: self.EffectType == "Damage", icon: "fa-solid fa-chevrons-up"){
                chat RollResult {
                    flavor("Talent activated! See effect.")
                    self.Effect
                    tag self.Activation
                }
            }
        action RollDamage(hidden: self.EffectType == "General", disabled: self.EffectType == "General", icon: "fa-solid fa-burst"){
                fleeting Prompt = prompt(label: "Clash Roll Modifiers") {
                    string Difficulty(choices: ["cs>=2","cs>=3","cs>=4","cs>=5"])
                    string Flanking(choices:["Difficulty -1 step if enemy Flanked."])
                    string Holding(choices:["Difficulty +1 step if enemy is Holding."])
                }
                fleeting rolltext = self.Dice + "d6x6" + Prompt.Difficulty
                fleeting Roll = roll(rolltext)
                if (parent is Army) {
                    parent.Stamina--
                    }
                chat RollResult {
                    flavor("Rolling Damage check. Stamina reduced by 1.")
                    Roll
                    self.Effect
                    tag self.Activation
                }
            }
    }
}